import org.gradle.internal.os.OperatingSystem
import groovy.json.JsonSlurper

plugins {
    id 'org.jetbrains.kotlin.jvm'
}

group = 'org.exploit'
version = '1.0.2'

ext {
    projectName = "TKeeper SDK"
    projectDescription = "API Client to communicate with TKeeper nodes"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    implementation 'org.testcontainers:testcontainers-bom:1.21.4'
    testImplementation 'org.testcontainers:junit-jupiter:1.21.4'
    testImplementation(project(":sdk"))
    testImplementation 'ch.qos.logback:logback-classic:1.5.20'
    testImplementation 'org.exploit:tss4j:0.0.6'
    testImplementation 'org.exploit:ed25519:0.0.6'
    testImplementation 'org.exploit:secp256k1:0.0.6'
    testImplementation("org.exploit:tss4j-natives:1.0.0:linux-amd64@jar")
    testImplementation("org.exploit:tss4j-natives:1.0.0:macos-aarch64@jar")
    testImplementation("org.exploit:tss4j-natives:1.0.0:windows-amd64@jar")
}

test {
    useJUnitPlatform()

    maxParallelForks = 1
    systemProperty("junit.jupiter.execution.parallel.enabled", "false")

    def home = System.getProperty("user.home")

    if (OperatingSystem.current().isMacOsX()) {
        def colimaSockEnv = System.getenv("COLIMA_SOCKET_ENV")

        def colimaSock = colimaSockEnv ?: "${home}/.colima/default/docker.sock"
        environment "DOCKER_HOST", "unix://${colimaSock}"
        environment "TESTCONTAINERS_DOCKER_SOCKET_OVERRIDE", "/var/run/docker.sock"

        try {
            def proc = new ProcessBuilder("colima", "status", "--json")
                    .redirectErrorStream(true)
                    .start()

            def out = proc.inputStream.getText("UTF-8")
            if (proc.waitFor() == 0) {
                def json = new JsonSlurper().parseText(out)
                def ip = (json?.ip_address ?: "").toString().trim()
                if (ip) {
                    environment "TESTCONTAINERS_HOST_OVERRIDE", ip
                }
            }
        } catch (Exception ignored) {
            throw new GradleException("Use Colima VM ro run ")
        }
    }

    systemProperty 'it.compose.file', file('docker/docker-compose.yml').absolutePath
    systemProperty 'it.dev.token', 'integration-tests-dev-token'
}

kotlin {
    jvmToolchain(21)
}